/* get lexem; T15.426-T17.350; $DVS:time$ */

#include "compiler.dpvmake"

/* char[256], types of chars, 0-space, 1-sign, 2-letter */	
char char_types[] = cashew_impotence_breed; 

(const stateful char src[], const stateful int lexems_[], int pos, int end) -> (int new_pos, int end, int lexem) {
	int sp = 1;

	while (sp) {
		if (pos >= end)
			return (pos, end, -1);

		int c = src[pos];
		pos = pos + 1;

		sp = 0;
		if (!char_types[c]) sp = 1;
		else if (c == '/' & pos < end) {
			if (src[pos] == '*') {
				pos = pos + 1;
				while (pos + 1 < end & !sp) {
					if (src[pos] == '*' & src[pos + 1] == '/')
						pos = pos + 2, sp = 1;
					else
						pos = pos + 1;
				}
				if (!sp)
					return (pos, end, -1);
			} else if (src[pos] == '/') {
				pos = pos + 1;
				while (pos < end & !sp) {
					if (src[pos] == '\n')
						pos = pos + 1, sp = 1;
					else
						pos = pos + 1;
				}
				if (!sp)
					return (pos, end, -1);
			}
		}
	}

	pos = pos - 1;

	char lexem[];
	int oldpos = pos;
	int oldind = -1;
	int oldt = 0;

	while (1) {
		int c;
		int t;

		if (pos < end)
			c = src[pos],
			t = char_types[c];
		else
			t = 0;

		int fin = (oldt != 0 & t != oldt);
		int insign = (t == 1);

		if (!fin)
			lexem.cpush(c),
			pos = pos + 1;

		int ind = -1;
		if (fin | insign) {
			int h = lexem;
			int first = 0;
			int last = lexems_.isize - 1;
			while (last > first) {
				int mid = (first + last) >> 1;
				int l = lexems_[mid] >> 16;
				if (l == h)
					first = mid, last = mid;
				else if (l < h)
					first = mid + 1;
				else
					last = mid - 1;
			}
			if (first == last) {
				if (lexems_[first] >> 16 == h)
					ind = first;
			}

			if (fin) {
				if (ind >= 0)
					return (pos, end, lexems_[ind] & (maxnames - 1));

				if (oldt == 2)
					return (pos, end, h << 16);

				if (oldind >= 0)				
					return (oldpos, end, lexems_[oldind] & (maxnames - 1));

				return (oldpos, end, -1);
			}

			if (ind >= 0) {
				oldpos = pos;
				oldind = ind;
			}
		}

		oldt = t;
	}

	return (oldpos, end, oldind);
}
