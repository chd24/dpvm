/* restricted view current entry command, T18.320-T18.336; $DVS:time$ */

#include "../../../stdlib/stdlib.dpvmake"
#include "../internal.dpvmh"

(volatile stateful char str[]) -> () hidePasswords {
	int i;
	for (i = 0; i < str.csize; i += 1) {
		int begin;
		int end;
		(begin, end) = strtostr(str, i, str.csize);

		int j;
		int waseq;
		for (j = begin; j < end; j += 1) {
			int c = str[j];
			if (!waseq && str[j] == '=') {
				begin = j + 1;
				waseq = 1;
			} else if (c == '.' || c == '-' || c == '@' || c == '/' || (j == end - 1 && (c == ',' || c == ':' || c == ';')))
				;
			else if (c < 'A' || c > 'Z' && c < 'a' || c > 'z' && c < 0x80 || (j > begin && c >= 'A' && c <= 'Z')) {
				for (j = begin; j < end; j += 1) {
					c = str[j];
					if (c >= 0x80 && c < 0xC0) {
						int k;
						for (k = j; k < str.csize - 1; k += 1)
							str[k] = str[k + 1];
						str.cpop(1);
						j -= 1;
						end -= 1;
					} else
						str[j] = '*';
				}
			}
		}

		i = end;
	}

	return;
}

(volatile stateful teData data) -> (const char result[]) commandView {
	if (data.pos + 144 > data.file.isize)
		return "End of database.\r\n";

	char res[];

	int i;
	for (i = 0; i < 4; i += 1) {
		res.cpush("10LE"[i]);
		strcat(res, ": [");

		char str[];

		int j;
		int base = data.pos + 16 + i * 32;
		for (j = 0; j < 64; j += 1) {
			int uni = data.file[base + (j >> 1)] >> ((j & 1) << 4) & 0xffff;
			if (uni < ' ')
				uni = ' ';
			else if (uni == 0xffff)
				uni = ' ';
			printutf8(str, uni);
		}

		hidePasswords(str);
		strcat(res, str);
		strcat(res, "]\r\n");
	}

	if (!data.history.isize || data.history[data.history.isize - 1] != data.pos)
		data.history.ipush(data.pos);

	return res;
}

teCommand cmdView = {
        "view",
        "view current database entry with hiding passwords",
        commandView
};


