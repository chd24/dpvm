/* kraken class */

int _Z = 0;
#include "../../../../../common/sysParams.dpvmh"
#include "../../../../../common/taskFlags.dpvmh"
#include "../../../../../lib/stdlib/stdlib.dpvmake"
#include "../../../../../lib/utils/utils.dpvmake"
#include "../kraken.dpvmake"

char version[] = "listOrders demo 0.1.0, T19.996-T20.035"; /* $DVS:time$ */

type demoData = {
	const char publicKey[];
	volatile stateful krakenClass krakenObj;
	volatile char txids[][];
	const char currencyPair[];
};

(volatile stateful any d, const char error[]) -> () getCallback = {
	demoData darr[];
	pushObject(d, darr);
	demoData data = darr[0];

	char text[];
	if (error.csize)
		printf(text, "%s", {error});
	else {
		int i, size = data.txids.lsize;
		printf(text, "%d orders opened:", {size});
		for (i = 0; i < size; i += 1) {
			if (i)
				printf(text, ",", {});
			printf(text, " %s", {data.txids[i]});
		}
	}
	printf(text, "\r\n", {});

	outputp("", "", text);
	return;
};

(volatile stateful demoData data, const char text[], int status) -> () inputCallback = {
	char error[];
	if (status <= 0)
		error = "Error: empty private key.";
	else
		error = data.krakenObj.methods.setKey(data.krakenObj, data.publicKey, text);

	if (error.csize) {
		outputp("", "", error);
		outputp("", "", "\r\n");
		return;
	}

	data.krakenObj.methods.listOrders(data.txids, data.krakenObj, getCallback, data, data.currencyPair);
	return;
};

(const char publicKey[], const char currencyPair[]) -> () listOrders = {
	krakenClass krakenObj = krakenCreate();
	char txids[][];
	demoData data = {publicKey, krakenObj, txids, currencyPair};

	const int vars[] = {DPVM_SYS_PARAM_FLAGS};
	const int flags = DPVM_TASK_FLAG_NO_INPUT_HISTORY + DPVM_TASK_FLAG_NO_INPUT_ECHO;
	const int values[] = {flags};
	setsysp("", "", vars, values);

	outputp("", "", "Private key: ");

	input(inputCallback, data, 256, -1);
	return;
};
