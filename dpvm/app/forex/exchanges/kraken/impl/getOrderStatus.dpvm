/* kraken class, getOrderStatus() method implementation, T19.829-T20.075; $DVS:time$ */

int _Z = 0;
#include "../../../../../common/sysParams.dpvmh"
#include "../../../../../common/mpopenFlags.dpvmh"
#include "../../../../../lib/stdlib/stdlib.dpvmake"
#include "../../../../../lib/math/crypto/json/json.dpvmake"
#include "kraken.dpvmake"

int TIMEOUT = 60000000000;

type requestData = {
	volatile stateful krakenImpl impl;
	const (volatile stateful any data, const char error[]) -> () callback;
	volatile stateful any data;
	const char parameters[];
	const char txid[];
	volatile stateful int status[];
	volatile char text[];
};

(volatile stateful requestData d, const char text[], int status) -> () inputCallback = {
	if (status < 0 && !d.text.csize) {
		char error[];
		printf(error, "Input failed: error %d.", {status});
		d.callback(d.data, error);
		return;
	}
	if (status > 0) {
		strcat(d.text, text);
		input(code, d, 0x100000, TIMEOUT);
		return;
	}
	if (!d.text.csize) {
		d.callback(d.data, "Empty output.");
		return;
	}

	jsonClass json = jsonCreate();
	jsonMethods j = json.methods;
	jsonObject obj;
	char error[];
	int next;
	(obj, error, next) = j.deserialize(d.text, 0, d.text.csize);
	if (error.csize) {
		d.callback(d.data, error);
		return;
	}
	jsonObject err = j.getObjectByIndex(j.getObjectByName(obj, "error"), 0);
	if (!j.isNull(err)) {
		char e[];
		error = j.getString(e, err);
		if (strstr(error, "Invalid order")) {
			d.status.ipush(ORDER_STATUS_UNKNOWN);
			d.callback(d.data, "");
		} else if (error.csize)
			d.callback(d.data, error);
		else
			d.callback(d.data, e);
		return;
	}

	jsonObject tx = j.getObjectByName(j.getObjectByName(obj, "result"), d.txid);
	if (j.isNull(tx)) {
		d.status.ipush(ORDER_STATUS_UNKNOWN);
		d.callback(d.data, "");
		return;
	}
	char str[];
	error = j.getString(str, j.getObjectByName(tx, "status"));
	if (error.csize) {
		d.callback(d.data, error);
		return;
	}
	     if (str == "pending" ) status = ORDER_STATUS_PENDING;
	else if (str == "open"    ) status = ORDER_STATUS_OPEN;
	else if (str == "closed"  ) status = ORDER_STATUS_EXECUTED;
	else if (str == "canceled") status = ORDER_STATUS_CANCELED;
	else if (str == "expired" ) status = ORDER_STATUS_EXPIRED;
	else {
		d.callback(d.data, "Unknown order status");
		return;
	}

	d.status.ipush(status);
	d.callback(d.data, "");
	return;
};

(volatile stateful requestData d, int status) -> () mpopenCallback = {
	if (status != DPVM_MPOPEN_READ) {
		char error[];
		printf(error, "Mpopen failed: error %d.", {status});
		d.callback(d.data, error);
		return;
	}
	char text[];
	d.text = text;
	input(inputCallback, d, 0x100000, TIMEOUT);
	return;
};

(volatile stateful requestData d, const int values[]) -> () timeCallback = {
	d.impl.data.nonce = values[0];

	char args[][];
	char error[];
	(args, error) = formPrivateQuery(d.impl, "QueryOrders", d.parameters);
	mpopen(mpopenCallback, d, WGET_BIN, args, DPVM_MPOPEN_READ);
	return;
};

(volatile stateful int status[], volatile stateful any krakenObj,
		const (volatile stateful any data, const char error[]) -> () callback, volatile stateful any data,
		const char txid[]) -> () getOrderStatus = {
	krakenImpl impl;
	char err[] = convertObjToImpl(impl, krakenObj, "getOrderStatus");
	if (err.csize) {
		callback(data, err);
		return;
	}

	char parameters[];
	printf(parameters, "txid=%s", {txid});

	char text[];
	requestData d = {impl, callback, data, parameters, txid, status, text};

	const int vars[] = {DPVM_SYS_PARAM_TIME};
	int values[] = {impl.data.nonce + REQUEST_INTERVAL};
	setsys(timeCallback, d, vars, values);
	return;

}
